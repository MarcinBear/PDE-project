{
 "cells": [
  {
   "cell_type": "markdown",
   "metadata": {},
   "source": [
    "<div class=\"text_center\", style=\"font-family: 'Times New Roman', Times, serif;\n",
    "                                 text-align: center;\n",
    "                                 font-weight: normal;\n",
    "                                 font-size: 18px;\n",
    "                                 padding: 0px 100px 0px 0px;\">\n",
    "    <span style=\"font-size: 15px; font-style: italic;\">\n",
    "        Partial Differential Equations with Applications in Physics and Industry\n",
    "    </span>\n",
    "    <hr>\n",
    "    <br>\n",
    "    <br> \n",
    "    <span style=\"font-size: 41px;\"> Wave equation for vibrating membrane </span>\n",
    "    <br>\n",
    "    <br>\n",
    "    <br>\n",
    "    Marcin Kostrzewa, &nbsp;Marcin Miśkiewicz\n",
    "</div>"
   ]
  },
  {
   "cell_type": "markdown",
   "metadata": {},
   "source": [
    "<div class=\"text\", style=\"font-family: 'Times New Roman', Times, serif;\n",
    "                          font-weight: normal;\n",
    "                          text-align: justify;\n",
    "                          font-size: 18px;\n",
    "                          padding: 0px 100px 0px 0px;\">\n",
    "    <h2>Abstract</h2>\n",
    "    <hr>\n",
    "    Lorem ipsum dolor sit amet, consectetur adipiscing elit, sed do eiusmod tempor incididunt ut labore et dolore magna aliqua. Ut enim ad minim veniam, quis nostrud exercitation ullamco laboris nisi ut aliquip ex ea commodo consequat. Duis aute irure dolor in reprehenderit in voluptate velit esse cillum dolore eu fugiat nulla pariatur. Excepteur sint occaecat cupidatat non proident, sunt in culpa qui officia deserunt mollit anim id est laborum.\n",
    "</div>"
   ]
  },
  {
   "cell_type": "markdown",
   "metadata": {},
   "source": [
    "<div class=\"text\", style=\"font-family: 'Times New Roman', Times, serif;\n",
    "                          font-weight: normal;\n",
    "                          text-align: justify;\n",
    "                          font-size: 18px;\n",
    "                          padding: 0px 100px 0px 0px;\">\n",
    "    \n",
    "<h2>Rectangular membrane</h2>\n",
    "<hr>\n",
    "\n",
    "Lorem ipsum...\n",
    "    \n",
    "$$\\begin{cases}\n",
    "u_{tt}       = c^2(u_{xx} + u_{yy}),  &(x, y) \\in \\Omega, \\; t \\in (0, T],  \\tag{$\\ast$} \\\\\n",
    "u(x, y, 0)   = I(x, y),               &(x, y) \\in \\Omega, \\\\\n",
    "u_t(x, y, 0) = V(x, y),               &(x, y) \\in \\Omega, \\\\\n",
    "u(x, y, t)   = 0,                     &(x, y) \\in \\partial\\Omega, \\; t \\in (0, T] .\n",
    "\\end{cases}$$\n",
    "    \n",
    "where $\\Omega \\subset \\mathbb{R}^d$ is in our case a rectangle $[A, B] \\times [C, D]$, $\\partial \\Omega$ is it's boundary, and $ c $ is a positive constant related to the speed of wave propagation in a given medium.\n",
    "\n",
    "</div>"
   ]
  },
  {
   "cell_type": "markdown",
   "metadata": {},
   "source": [
    "<div class=\"text\", style=\"font-family: 'Times New Roman', Times, serif;\n",
    "                          font-weight: normal;\n",
    "                          text-align: justify;\n",
    "                          font-size: 18px;\n",
    "                          padding: 0px 100px 0px 0px;\">\n",
    "    <h2>Finite difference method</h2>\n",
    "    <hr>\n",
    "\n",
    "In order to obtain a numerical soulution of $(\\ast)$, we need to discretize the problem. First, let us assume that time points are $t_0 = 0 < t_1 < \\ldots < t_{N_T}$ and for all $n = 1, 2\\ldots, N$ we have $t_n - t_{n-1} = \\Delta t$. For the spatial variables we introduce a rectangular mesh  \n",
    "    \n",
    "$$A = x_0 < x_1 < \\ldots < x_{N_x} = B, \\\\ \n",
    "C = y_0 < y_1 < \\ldots < y_{N_y} = D,$$\n",
    "    \n",
    "where $x_i - x_{i-1} = \\Delta x$ and $y_j - y_{j-1} = \\Delta y$. From this point, we will denote the value of $u$ for $(x_i, y_j, t_n)$ as $u_{i,j}^n$.\n",
    "</div>"
   ]
  },
  {
   "cell_type": "markdown",
   "metadata": {},
   "source": [
    "<div class=\"caption\", style=\"font-family: 'Times New Roman', Times, serif;\n",
    "                              font-weight: normal;\n",
    "                              text-align: center;\n",
    "                              font-size: 18px;\n",
    "                              padding: 0px 100px 20px 0px;\">\n",
    "    \n",
    "<img src=\"./media/mesh.png\" width=\"600\">\n",
    "<br>\n",
    "Figure: rectangular mesh for spatial variables.\n",
    "    \n",
    "</div>"
   ]
  },
  {
   "cell_type": "markdown",
   "metadata": {},
   "source": [
    "<div class=\"text\", style=\"font-family: 'Times New Roman', Times, serif;\n",
    "                          font-weight: normal;\n",
    "                          text-align: justify;\n",
    "                          font-size: 18px;\n",
    "                          padding: 0px 100px 0px 0px;\">\n",
    "\n",
    "We can discretize $(\\ast)$ using the second order central difference approximation given by\n",
    "    \n",
    "    \n",
    "$$f''(x) \\approx \\frac{\\frac{f(x + h) - f(x)}{h} - \\frac{f(x) - f(x-h)}{h}}{h} = \\frac{f(x+h) - 2f(x) + f(x-h)}{h^2}.$$\n",
    "    \n",
    "Now we obtain    \n",
    "\n",
    "$$\\frac{u_{i,j}^{n+1} - 2u_{i,j}^n + u_{i,j}^{n-1}}{(\\Delta t)^2} = c^2\\frac{u_{i+1,j}^{n} - 2u_{i,j}^n + u_{i-1,j}^{n}}{(\\Delta x)^2} + c^2\\frac{u_{i,j+1}^{n} - 2u_{i,j}^n + u_{i,j-1}^{n}}{(\\Delta y)^2}$$\n",
    "\n",
    "\n",
    "for $n > 0$, $i >0$, $j > 0$. Letting $r_x = c\\frac{\\Delta t}{\\Delta x}$ and $r_y = c\\frac{\\Delta t}{\\Delta y}$ we can rewrite the above equation as\n",
    "\n",
    "    \n",
    "<!-- $$u_{i,j}^{n+1} = u_{i, j}^n\\left(2 - 2\\left(r_x^2 + r_y^2\\right)\\right) + r_x^2\\left(u_{i+1, j}^n + u_{i-1, j}^n\\right) + r_y^2\\left(u_{i, j+1}^n + u_{i, j-1}^n\\right) - u_{i,j}^{n-1}.$$ -->\n",
    "    \n",
    "$$u_{i,j}^{n+1} = 2u_{i, j}^n  - u_{i,j}^{n-1} + r_x^2\\left(u_{i+1, j}^n -2u_{i, j}^n + u_{i-1, j}^n\\right) + r_y^2\\left(u_{i, j+1}^n -2u_{i, j}^n + u_{i, j-1}^n\\right). \\tag{$\\star$}$$\n",
    "    \n",
    "<h3>Initial conditions</h3>\n",
    "    \n",
    "For the first initial condition, we simply have\n",
    "\n",
    "$$u^0_{i, j} = I(x_i, y_j).$$\n",
    "    \n",
    "Note that for the second initial condition\n",
    "    \n",
    "$$V(x_i, y_j) = \\frac{u_{i, j}^1 - u_{i, j}^{-1}}{2\\Delta t} \\; \\Longrightarrow \\; u_{i,j}^{-1} = u_{i,j}^1 - 2\\Delta t V(x_i, y_j)$$\n",
    "\n",
    "and by combining above equation with $(\\star)$ for $n=0$ we can get rid of virtual/ghost values $u_{i, j}^{-1}$, eventually obtaining\n",
    "    \n",
    "$$u_{i,j}^{1} = u_{i, j}^0  + \\Delta t V_{i, j} + \\frac{r_x^2}{2}\\left(u_{i+1, j}^0 -2u_{i, j}^0 + u_{i-1, j}^n\\right) + \\frac{r_y^2}{2}\\left(u_{i, j+1}^0 -2u_{i, j}^0 + u_{i, j-1}^0\\right).$$\n",
    "    \n",
    "Therefore, we should compute $u_{i,j}^0$ and $u_{i, j}^1$ separately, and then use $(\\star)$ for $u^n_{i, j}$, $n = 2, 3,\\ldots, N_t.$ \n",
    "    \n",
    "<h3>Boundary conditions</h3>\n",
    "\n",
    "For the boundary conditions we have\n",
    "    \n",
    "$$\\begin{cases}\n",
    "u_{0,j}^n = u(A, y_j, n) = 0, \\\\\n",
    "u_{i,0}^n = u(x_i, C, n) = 0, \\\\\n",
    "u_{N_x,j}^n = u(B, y_j, n) = 0, \\\\\n",
    "u_{i,N_y}^n = u(x_i, D, n) = 0. \n",
    "\\end{cases} \\qquad \\text{for } \\; n = 0, 1 \\ldots, N.$$\n",
    "\n",
    "</div>"
   ]
  },
  {
   "cell_type": "markdown",
   "metadata": {},
   "source": [
    "<div class=\"text\", style=\"font-family: 'Times New Roman', Times, serif;\n",
    "                          font-weight: normal;\n",
    "                          text-align: justify;\n",
    "                          font-size: 18px;\n",
    "                          padding: 0px 100px 0px 0px;\">\n",
    "    <h2>Results</h2>\n",
    "    <hr>\n",
    "    Lorem ipsum dolor sit amet, consectetur adipiscing elit, sed do eiusmod tempor incididunt ut labore et dolore magna aliqua. Ut enim ad minim veniam, quis nostrud exercitation ullamco laboris nisi ut aliquip ex ea commodo consequat. Duis aute irure dolor in reprehenderit in voluptate velit esse cillum dolore eu fugiat nulla pariatur. Excepteur sint occaecat cupidatat non proident, sunt in culpa qui officia deserunt mollit anim id est laborum. <a href=#1>[1]</a>\n",
    "</div>"
   ]
  },
  {
   "cell_type": "code",
   "execution_count": 1,
   "metadata": {},
   "outputs": [],
   "source": [
    "\n",
    "\n",
    "using Plots, Measures, LinearAlgebra, Logging, LaTeXStrings\n",
    "Logging.disable_logging(Logging.Info); # disable ugly messages after generating animations\n",
    "\n",
    "# plotting constants\n",
    "COLORMAP_1 = cgrad([:midnightblue, :purple, :purple, :skyblue, :cyan, :cyan, :white])\n",
    "SIZE_1 = (1000, 500);\n",
    "\n"
   ]
  },
  {
   "cell_type": "code",
   "execution_count": 2,
   "metadata": {},
   "outputs": [],
   "source": [
    "\n",
    "\n",
    "function grid(; Δx::Float64, Δy::Float64, Ω::Dict) \n",
    "    A, B, C, D = Ω[\"A\"], Ω[\"B\"], Ω[\"C\"], Ω[\"D\"]\n",
    "    xs = A:Δx:B\n",
    "    ys = C:Δy:D\n",
    "    nx, ny = length(xs), length(ys)\n",
    "    xx = xs' .* ones(ny)\n",
    "    yy = ys  .* ones(nx)'\n",
    "    return (nx, ny, xx, yy)\n",
    "end;\n",
    "\n"
   ]
  },
  {
   "cell_type": "code",
   "execution_count": 3,
   "metadata": {},
   "outputs": [],
   "source": [
    "\n",
    "\n",
    "function explicit_finite_difference(; Δx::Float64, \n",
    "                                      Δy::Float64, \n",
    "                                      Δt::Float64, \n",
    "                                      c::Float64, \n",
    "                                      N::Int64,\n",
    "                                      ICs::Dict, \n",
    "                                      BCs::Dict, \n",
    "                                      grid_params::Tuple)\n",
    "\n",
    "    (nx, ny, xx, yy) = grid_params\n",
    "    i = collect(2:nx-1)\n",
    "    j = collect(2:ny-1)\n",
    "    \n",
    "    u_Ay, u_By = BCs[\"u(A, y, t)\"], BCs[\"u(B, y, t)\"]\n",
    "    u_xC, u_xD = BCs[\"u(x, C, t)\"], BCs[\"u(x, D, t)\"]\n",
    "    I, V = ICs[\"I(x, y)\"], ICs[\"V(x, y)\"]\n",
    "\n",
    "    rx = c * Δt / Δx\n",
    "    ry = c * Δt / Δy\n",
    "    r = rx + ry\n",
    "\n",
    "    if r < 1/√2 # checking if method is stable\n",
    "        (@warn \"CFL stability condition is not met! (r = $r < 1/√2)\")\n",
    "    end\n",
    "\n",
    "    u = zeros(ny, nx, N)\n",
    "    u[:,  :, 1] = I.(xx, yy)                                                        #   <- first IC (t = 0)\n",
    "    u[i,  j, 2] = (u[i, j, 1] .+ Δt .* V.(xx, yy)[i, j]                             # }\n",
    "                .+ rx^2/2 * (u[i.+1, j, 1] .- 2 .* u[i, j, 1] .+ u[i.-1, j, 1])     # } <- second IC (t = 1)\n",
    "                .+ ry^2/2 * (u[i, j.+1, 1] .- 2 .* u[i, j, 1] .+ u[i, j.-1, 1]))    # }\n",
    "\n",
    "    u[1,  :, 1:2] .= u_Ay  # }\n",
    "    u[:,  1, 1:2] .= u_xC  # } <- putting BCs\n",
    "    u[ny, :, 1:2] .= u_By  # }\n",
    "    u[:, nx, 1:2] .= u_xD  # }\n",
    "\n",
    "    for t in 3:N # Solving for t = 2, 3, ..., T\n",
    "        u[i, j, t] = (2 .* u[i, j, t.-1] .- u[i, j, t.-2]\n",
    "                    .+ rx^2 * (u[i.+1, j, t.-1] .- 2 .* u[i, j, t.-1] .+ u[i.-1, j, t.-1]) \n",
    "                    .+ ry^2 * (u[i, j.+1, t.-1] .- 2 .* u[i, j, t.-1] .+ u[i, j.-1, t.-1]))              \n",
    "    end\n",
    "    return u\n",
    "end;\n",
    "\n"
   ]
  },
  {
   "cell_type": "code",
   "execution_count": 4,
   "metadata": {},
   "outputs": [],
   "source": [
    "\n",
    "\n",
    "ICs = Dict(  # Initial Conditions\n",
    "     \"I(x, y)\" => (x, y) -> exp(-x^2 - y^2),\n",
    "     \"V(x, y)\" => (x, y) -> 0 #-exp(-x^2 - y^2)\n",
    ")\n",
    "\n",
    "\n",
    "BCs = Dict(  # Dirichlet Boundary Conditions\n",
    "    \"u(A, y, t)\" => 0,\n",
    "    \"u(B, y, t)\" => 0,\n",
    "    \"u(x, C, t)\" => 0,\n",
    "    \"u(x, D, t)\" => 0,\n",
    ")\n",
    "\n",
    "Ω = Dict(  # Domain - rectangle\n",
    "    \"A\" => -5,  # x min\n",
    "    \"B\" =>  5,  # x max\n",
    "    \"C\" => -5,  # y min\n",
    "    \"D\" =>  5,  # y max\n",
    ")\n",
    "\n",
    "\n",
    "Δx = 0.1 \n",
    "Δy = 0.1\n",
    "Δt = 0.1\n",
    "c  = 0.5\n",
    "N  = 700\n",
    "\n",
    "grid_params = grid(Δx=Δx, Δy=Δy, Ω=Ω)\n",
    "u = explicit_finite_difference(Δx=Δx, Δy=Δy, Δt=Δt, c=c, N=N, ICs=ICs, BCs=BCs, grid_params=grid_params);\n",
    "\n"
   ]
  },
  {
   "cell_type": "code",
   "execution_count": 5,
   "metadata": {},
   "outputs": [],
   "source": [
    "\n",
    "\n",
    "function wave_animation(u::Array{Float64, 3}, \n",
    "                        grid_params::Tuple; \n",
    "                        plot_size::Tuple=SIZE_1, \n",
    "                        colormap=COLORMAP_1, \n",
    "                        title = \"\", \n",
    "                        kwargs...)\n",
    "\n",
    "    frames = size(u)[3]\n",
    "    (nx, ny, xx, yy) = grid_params\n",
    "    xs, ys = xx[1, :], yy[:, 1]\n",
    "    l = @layout [a{0.6w} b]\n",
    "    camera_angles = LinRange(0, 360, frames)\n",
    "\n",
    "    an = @animate for t in 1:frames\n",
    "\n",
    "        p1 = surface(xx, yy, u[:, :, t],\n",
    "                    zlims=(extrema(u)),\n",
    "                    colormap=colormap,\n",
    "                    colorbar=false,\n",
    "                    zlabel=L\"u\")\n",
    "\n",
    "        p2 = heatmap(xs, ys, u[:, :, t], \n",
    "                    colormap=colormap)\n",
    "\n",
    "        plot(p1, p2, \n",
    "            layout=l, \n",
    "            xlabel=L\"x\",\n",
    "            ylabel=L\"y\",\n",
    "            clims=extrema(u),\n",
    "            xlims=extrema(xs),\n",
    "            ylims=extrema(ys), \n",
    "            camera=(camera_angles[t], 30), \n",
    "            plot_title= title * \"\\n\\n\" * latexstring(\" (t = $(round(t*Δt, digits=2)))\"),\n",
    "            aspect_ratio=:equal, \n",
    "            size=plot_size,\n",
    "            link=:all;\n",
    "            kwargs...)\n",
    "    end\n",
    "    return an\n",
    "end;\n",
    "\n"
   ]
  },
  {
   "cell_type": "code",
   "execution_count": 6,
   "metadata": {},
   "outputs": [],
   "source": [
    "\n",
    "\n",
    "an1 = wave_animation(u[:, :, 1:500], grid_params, title = L\"u(x, y, 0) = e^{-(x^2 + y^2)}, \\quad u_t(x, y, 0) = 0\")\n",
    "# gif(an1, \"./media/test.gif\", fps = 25);\n",
    "\n"
   ]
  },
  {
   "cell_type": "markdown",
   "metadata": {},
   "source": [
    "<img src=\"./media/test.gif\">"
   ]
  },
  {
   "cell_type": "markdown",
   "metadata": {},
   "source": [
    "<div class=\"text\", style=\"font-family: 'Times New Roman', Times, serif;\n",
    "                          font-weight: normal;\n",
    "                          text-align: justify;\n",
    "                          font-size: 18px;\n",
    "                          padding: 0px 100px 0px 0px;\">\n",
    "    <h2>References</h2>\n",
    "    <hr>\n",
    "[1] &nbsp;<a id=\"1\", href = http://hplgit.github.io/num-methods-for-PDEs/doc/pub/wave/pdf/wave-4print.pdf>Hans Petter Langtangen, Finite difference methods for wave\n",
    "motion (2016)</a>\n",
    "<br>\n",
    "[2]&nbsp; <a id=\"2\", href = https://www.google.com/>empty</a>\n",
    "<br>\n",
    "[3]&nbsp; <a id=\"3\", href = https://www.google.com/>empty</a>\n",
    "<br>\n",
    "[4]&nbsp; <a id=\"4\", href = https://www.google.com/>empty</a>\n",
    "<br>\n",
    "[5]&nbsp; <a id=\"5\", href = https://www.google.com/>empty</a>\n",
    "</div>"
   ]
  },
  {
   "cell_type": "code",
   "execution_count": null,
   "metadata": {},
   "outputs": [],
   "source": []
  }
 ],
 "metadata": {
  "kernelspec": {
   "display_name": "Julia 1.8.5",
   "language": "julia",
   "name": "julia-1.8"
  },
  "language_info": {
   "file_extension": ".jl",
   "mimetype": "application/julia",
   "name": "julia",
   "version": "1.8.5"
  }
 },
 "nbformat": 4,
 "nbformat_minor": 2
}
